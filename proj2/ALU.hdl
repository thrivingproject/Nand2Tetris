// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // Logical not the x input?
        zy, // zero the y input?
        ny, // Logical not the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // Logical not the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // Zero x, y
    Mux16(a=x, b[0..15]=false, sel=zx, out=x1);
    Mux16(a=y, b[0..15]=false, sel=zy, out=y1);

    // Not x, y
    Not16(in=x1, out=notx);
    Not16(in=y1, out=noty);

    // x2, y2
    Mux16(a=x1, b=notx, sel=nx, out=x2);
    Mux16(a=y1, b=noty, sel=ny, out=y2);

    // Function
    Add16(a=x2, b=y2, out=add16xy);
    And16(a=x2, b=y2, out=and16xy);
    Mux16(a=and16xy, b=add16xy, sel=f, out=tmp);

    // Not output
    Not16(in=tmp, out=nottmp);

    // out
    Mux16(
        a=tmp,
        b=nottmp,
        sel=no,
        out=out,
        out[0..7]=out1,
        out[8..15]=out2,
        out[15]=out3
    );

    // zr
    Or8Way(in=out1, out=out1or);
    Or8Way(in=out2, out=out2or);
    Or(a=out1or, b=out2or, out=notzr);
    Not(in=notzr, out=zr);

    // ng
    And(a=out3, b=true, out=ng);
}